abstract type VoterModel end
abstract type SpatialModel <: VoterModel end
abstract type SampleModel <: VoterModel end

struct RepDrawModel <: SampleModel
    base_electorate
end

struct ImpartialCulture <: VoterModel
    distribution
end

"""
A basic spatial model.
"""
struct DimModel <: SpatialModel
    ndims::Int
    voterdimstds
    candidatedimstds
end

struct BaseQualityNoiseModel <: VoterModel
    basemodel::VoterModel
    qualitystd::Float64
    noisestd::Float64
end
"""
An augmented version of base_model that adds voter-specific noise importance, preference exponents,
and importance of candidate quality.
"""
struct BQNEModel <: VoterModel
    base_model::VoterModel
    base_model_std::Float64
    quality_importance_distribution::Distributions.Distribution
    noise_importance_distribution::Distributions.Distribution
    exponent_distribution::Distributions.Distribution
end

function BQNEModel(quality_importance_distribution::Distributions.Distribution,
          noise_importance_distribution::Distributions.Distribution,
          exponent_distribution::Distributions.Distribution)
    BQNEModel(quinn, 0.4,
        Distributions.Truncated(quality_importance_distribution, 0.0, 1.0),
        Distributions.Truncated(noise_importance_distribution, 0.0, 1.0),
        exponent_distribution)
end
function BQNEModel(quality_importance_mean::Float64, quality_importance_std::Float64,
            noise_importance_mean::Float64, noise_importance_std::Float64,
            exponent_log_mean::Float64, exponent_log_std::Float64)
    noise_precision = noise_importance_mean*(1-noise_importance_mean)/(noise_importance_std^2) - 1
    noise_alpha = noise_importance_mean*noise_precision
    noise_beta = (1 - noise_importance_mean)*noise_precision
    quality_precision = quality_importance_mean*(1-quality_importance_mean)/(quality_importance_std^2) - 1
    quality_alpha = quality_importance_mean*quality_precision
    quality_beta = (1 - quality_importance_mean)*quality_precision
    BQNEModel(quinn, 0.4, Distributions.Beta(quality_alpha, quality_beta),
        Distributions.Beta(noise_alpha, noise_beta),
        Distributions.LogNormal(exponent_log_mean, exponent_log_std))
end

struct AugmentedQuinnModel <: VoterModel
    q_mean::Float64
    q_std::Float64
    n_mean::Float64
    n_std::Float64
    e_log_mean::Float64
    e_log_std::Float64
end

function make_electorate(model::AugmentedQuinnModel, nvot::Int, ncand::Int, seed::Int)
    if model.q_std == 0.0
        quality_importance_distribution = Distributions.Dirac(model.q_mean)
    else
        quality_precision = model.q_mean*(1-model.q_mean)/(model.q_std^2) - 1
        quality_alpha = model.q_mean*quality_precision
        quality_beta = (1 - model.q_mean)*quality_precision
        quality_importance_distribution = Distributions.Beta(quality_alpha, quality_beta)
    end
    if model.n_std == 0.0
        noise_importance_distribution = Distributions.Dirac(model.n_mean)
    else
        noise_precision = model.n_mean*(1-model.n_mean)/(model.n_std^2) - 1
        noise_alpha = model.n_mean*noise_precision
        noise_beta = (1 - model.n_mean)*noise_precision
        noise_importance_distribution = Distributions.Beta(noise_alpha, noise_beta)
    end
    return make_electorate(
        BQNEModel(quinn, 0.4, quality_importance_distribution,
            noise_importance_distribution,
            Distributions.LogNormal(model.e_log_mean, model.e_log_std)),
        nvot, ncand, seed)
end

struct ExpPreferenceModel <: VoterModel
    basemodel::VoterModel
    exponent::Float64
end

struct DCCModel <: SpatialModel
    viewdecaydist
    viewcut
    dimdecaydist
    dimcut
    clusteralpha
    caringdist
end

QuinnModel = DCCModel

"""
Always yields the same electorate; only useful for testing.
"""
struct TestModel <: VoterModel
    electorate
end

struct TestSpatialModel <: SpatialModel
    spatial_electorate
end

"""
Use the defaults from the Python VSE code.
"""
dcc = DCCModel(Distributions.Uniform(), 0.2,
                        Distributions.Uniform(), 0.2,
                        1, Distributions.Beta(6, 3))
quinn = dcc

"""
A container for containing both an electorate and the seed that generated it.
"""
struct SeededElectorate <: AbstractMatrix{Float64}
    data::Matrix{Float64}
    seed::Int
end

"""
Contains an electorate generated by a spatial model.

voter_coordinates[k, i] is where voter i stands on dimension k.
caring_matrix[k, i] is how much voter i cares about dimension k.
candidate_coordinates[k, j] is where candidate j stands on dimension k.
utility_matrix[i, j] is how much voter i likes candidate j.
seed is the random seed used to generate the electorate.
"""
struct SpatialElectorate
    voter_coordinates::Matrix{Float64}
    caring_matrix::Matrix{Float64}
    candidate_coordinates::Matrix{Float64}
    utility_matrix::Matrix{Float64}
    seed::Int
end

struct AugmentedElectorate <: AbstractMatrix{Float64}
    voter_coordinates::Matrix{Float64}
    caring_matrix::Matrix{Float64}
    candidate_coordinates::Matrix{Float64}
    utility_matrix::Matrix{Float64}
    intercandidate_utility_matrix::Matrix{Float64}
    candidate_qualities::Vector{Float64}
    seed::Int
end

Base.size(e::AugmentedElectorate) = size(e.utility_matrix)
Base.getindex(e::AugmentedElectorate, I::Vararg{Int,N}) where {N} = e.utility_matrix[I...]
Base.setindex!(e::AugmentedElectorate, v::Float64, I::Vararg{Int,N}) where N = (e.utility_matrix[I...] = v)
getseed(e::AugmentedElectorate) = e.seed

function SpatialElectorate(vc::Matrix{Float64}, cm::Matrix{Float64}, cc::Matrix{Float64}, seed::Int)
    um = make_utility_matrix(vc, cm, cc)
    SpatialElectorate(vc, cm, cc, um, seed)
end

Base.size(e::SeededElectorate) = size(e.data)
Base.getindex(e::SeededElectorate, I::Vararg{Int,N}) where {N} = e.data[I...]
Base.setindex!(e::SeededElectorate, v::Float64, I::Vararg{Int,N}) where N = (e.data[I...] = v)
getseed(e::SeededElectorate) = e.seed
getseed(::Any) = 0

DimModel(dim::Int) = DimModel(dim, ones(Int, dim), ones(Int, dim))
DimModel(dim::Int, weights::Vector{<:Real}) = DimModel(dim, weights, weights)
DimModel(dim::Int, candstd::Real) = DimModel(dim, ones(Int, dim), repeat([candstd], dim))

ic = ImpartialCulture(randn)

"""
    make_electorate(model::VoterModel, nvot::Int, ncand::Int)

Create an electorate according to the voter model. Creates a random seed.
"""
function make_electorate(model::VoterModel, nvot::Int, ncand::Int)
    seed = abs(rand(Int))
    make_electorate(model, nvot, ncand, seed)
end

function make_spatial_electorate(model::SpatialModel, nvot::Int, ncand::Int)
    seed = abs(rand(Int))
    make_spatial_electorate(model, nvot, ncand, seed)
end

function make_electorate(model::SpatialModel, nvot::Int, ncand::Int, seed::Int)
    spatial = make_spatial_electorate(model, nvot, ncand, seed)
    return SeededElectorate(spatial.utility_matrix, seed)
end

"""
    make_electorate(model::RepDrawModel, nvot::Int, ::Int, seed::Int)

Create an electorate in which voters are drawn with replacement from the basemodel.

Candidates are the same.
"""
function make_electorate(model::RepDrawModel, nvot::Int, ::Int, seed::Int)
    rng = Random.Xoshiro(seed)
    elec = Matrix{Float64}(undef, size(model.base_electorate, 1), nvot)
    voterpoolsize = size(model.base_electorate, 2)
    for i in 1:nvot
        elec[:, i] = model.base_electorate[:, rand(rng, 1:voterpoolsize)]
    end
    return SeededElectorate(elec, seed)
end

"""
    make_electorate(model::ImpartialCulture, nvot::Int, ncand::Int, seed::Int)

Create an electorate in which all preferences are uncorrelated. Very unrealistic.
"""
function make_electorate(model::ImpartialCulture, nvot::Int, ncand::Int, seed::Int)
    rng = Random.Xoshiro(seed)
    SeededElectorate(model.distribution(rng, ncand, nvot), seed)
end

function make_utility_matrix(voterpositions, caring_matrix, candpositions)
    nvot, ncand = (size(voterpositions, 2), size(candpositions, 2))
    ndims = size(voterpositions, 1)
    utilmatrix = Matrix{Float64}(undef, ncand, nvot)
    for (i, voter) in enumerate(eachcol(voterpositions))
        for (j, cand) in enumerate(eachcol(candpositions))
            utilmatrix[j, i] = -sqrt(
                sum((caring_matrix[k, i]*(voter[k] - cand[k]))^2 for k in 1:ndims))
        end
    end
    return utilmatrix
end

"""
    make_spatial_electorate(model::DimModel, nvot::Int, ncand::Int, seed::Int)

Create an electorate under a spatial model.

Utilities are minus the cartesian distance between a voter and a candidate.
"""
function make_spatial_electorate(model::DimModel, nvot::Int, ncand::Int, seed::Int)
    rng = Random.Xoshiro(seed)
    voterpositions = randn(rng, model.ndims, nvot).*model.voterdimstds
    candpositions = randn(rng, model.ndims, ncand).*model.candidatedimstds
    weights = ones(model.ndims, nvot)
    #utilmatrix = make_utility_matrix(voterpositions, weights, candpositions)
    return SpatialElectorate(voterpositions, weights, candpositions, seed)
end

"""
    make_electorate(model::BaseQualityNoiseModel, nvot::Int, ncand::Int, seed::Int)

Create an electorate from a base model with candidate quality and iid voter preferences included
"""
function make_electorate(model::BaseQualityNoiseModel, nvot::Int, ncand::Int, seed::Int)
    rng = Random.Xoshiro(seed)
    electorate = make_electorate(model.basemodel, nvot, ncand, seed)
    for c in 1:ncand
        electorate[c,:] .+= model.qualitystd*randn(rng)
    end
    electorate += model.noisestd*randn(rng, ncand, nvot)
end

function make_electorate(model::BQNEModel, nvot::Int, ncand::Int, seed::Int)
    rng = Random.Xoshiro(seed)
    electorate = make_electorate(model.base_model, nvot, ncand, seed)
    electorate.utility_matrix ./= model.base_model_std
    electorate.candidate_qualities .= randn(rng, ncand)
    for i in 1:nvot
        quality_importance = rand(rng, model.quality_importance_distribution)
        noise_importance = rand(rng, model.noise_importance_distribution)
        for c in 1:ncand
            electorate.utility_matrix[c, i] = (1-noise_importance)*(quality_importance*electorate.candidate_qualities[c]
                + (1-quality_importance)*electorate.utility_matrix[c, i]) + noise_importance*randn(rng)
        end
        exponent = rand(rng, model.exponent_distribution)
        electorate.utility_matrix[:, i] .-= minimum(electorate.utility_matrix[:, i]) #Make 0 the lowest utility
        electorate.utility_matrix[:, i] .^= exponent
        electorate.utility_matrix[:, i] ./= Statistics.std(electorate.utility_matrix[:, i]) #Give all voters the same standard deviation for their utilities
    end
    electorate.intercandidate_utility_matrix ./= model.base_model_std
    for i in 1:ncand
        quality_importance = rand(rng, model.quality_importance_distribution)
        electorate.intercandidate_utility_matrix[:, i] = (1 - quality_importance)*electorate.intercandidate_utility_matrix[:, i] .+
            quality_importance*electorate.candidate_qualities
    end
    #No noise or exponentiation for intercandidate utilities
    return electorate
end

function make_electorate(model::ExpPreferenceModel, nvot::Int, ncand::Int, seed::Int)
    base_electorate = make_electorate(model.basemodel, nvot, ncand, seed)
    normalized_electorate = Matrix{Float64}(undef, ncand, nvot)
    for (i, utils) in enumerate(eachcol(base_electorate))
        high, low = maximum(utils), minimum(utils)
        normalized_electorate[:, i] = (utils .- low)./(high-low)
    end
    return SeededElectorate(normalized_electorate .^ model.exponent, seed)
end

"""
    make_spatial_electorate(model::DCCModel, nvot::Int, ncand::Int, seed::Int)

Create an electorate under a Dirichlet CrossCat model.

See https://jmlr.org/papers/volume17/11-392/11-392.pdf for a description of CrossCat.

Designed and originally written in Python by Jameson Quinn.
"""
function make_spatial_electorate(model::DCCModel, nvot::Int, ncand::Int, seed::Int)
    rng = Random.Xoshiro(seed)
    viewdims, dimweights = makeviews(model, rng)
    nviews = length(viewdims)
    views, clustercounts = assignclusters(model, nvot + ncand, nviews, rng)
    clustermeans, clusterimportances = makeclusterprefs(model, nviews, viewdims, clustercounts, rng)
    votersandcands, weights = makeprefpoints(model, views, viewdims, dimweights, clustermeans, clusterimportances, nviews, rng)
    voterpositions = votersandcands[:, 1:nvot]
    candpositions = votersandcands[:, nvot+1:nvot+ncand]
    normalize_weights(model, weights)
    utilmatrix = make_utility_matrix(voterpositions, weights, candpositions)
    return SpatialElectorate(voterpositions, weights[:, 1:nvot], candpositions, utilmatrix, seed)
end

function make_electorate(model::DCCModel, nvot::Int, ncand::Int, seed::Int)
    rng = Random.Xoshiro(seed)
    viewdims, dimweights = makeviews(model, rng)
    nviews = length(viewdims)
    views, clustercounts = assignclusters(model, nvot + ncand, nviews, rng)
    clustermeans, clusterimportances = makeclusterprefs(model, nviews, viewdims, clustercounts, rng)
    votersandcands, weights = makeprefpoints(model, views, viewdims, dimweights, clustermeans, clusterimportances, nviews, rng)
    voterpositions = votersandcands[:, 1:nvot]
    candpositions = votersandcands[:, nvot+1:nvot+ncand]
    normalize_weights(model, weights)
    voter_util_matrix = make_utility_matrix(voterpositions, weights, candpositions)
    candidate_util_matrix = make_utility_matrix(candpositions, weights[:, nvot+1:nvot+ncand], candpositions)

    return AugmentedElectorate(voterpositions, weights[:, 1:nvot], candpositions,
        voter_util_matrix,
        candidate_util_matrix,
        zeros(Float64, ncand), #candidate qualities are not modeled in DCC
        seed)
end

"""
    makeviews(model::DCCModel, rng)

Create views for the CrossCat model and weight their relative importance.

Also creates dimensions wthin each view and weights the imprtance of each dimension.
Returns (viewdims, dimweights), where viewdims is a vector giving the number of dimensions
with each view, and dimweights is a vector with the weights giving the importance of each
dimension.
"""
function makeviews(model::DCCModel, rng)
    viewdims = Int[] #number of dimensions in each view
    dimweights = Float64[] #unnormalized raw importance of each dimension, regardless of view
    view_weight = 1
    while view_weight > model.viewcut
        dimweight = view_weight
        dimnum = 0
        while dimweight > model.dimcut
            append!(dimweights, dimweight)
            dimnum += 1
            dimweight *= rand(rng, model.dimdecaydist)
        end
        append!(viewdims, dimnum)
        view_weight *= rand(rng, model.viewdecaydist)
    end
    return (viewdims, dimweights)
end

"""
    assignclusters(model::DCCModel, npoints::Int, nviews::Int, rng)

Use a Chinese Restaurant model to assign voters and candidates to clusters for each view.

A point can represent a voter or a candidate.

Returns (views, clustercounts).
views is an (nviews by npoints) matrix in which views[view, point] gives the cluster index
assigned to the point for a given view.
clustercounts is a vector s.t. clustercounts[view] is the total number of clusters within
the given view.
"""
function assignclusters(model::DCCModel, npoints::Int, nviews::Int, rng)
    views = Matrix{Int}(undef, nviews, npoints)
    clustercounts = zeros(Int, nviews)
    for i in 1:npoints #voter and/or candidate index
        for view in 1:nviews
            r = (i - 1 + model.clusteralpha) * rand(rng)
            if r > i - 1
                clustercounts[view] += 1
                views[view, i] = clustercounts[view]
            else
                views[view, i] = views[view, floor(Int, r) + 1]
            end
        end
    end
    return views, clustercounts
end

"""
    makeclusterprefs(model::DCCModel, views::Matrix{Int}, viewdims::Vector{Int}, clustercounts::Int, rng)

Create a mean preference for each cluster within each view in each of the view's dimensions in "policyspace"

Returns (clustermeans, clusterimportances).
clustermeans[dim_within_view, cluster, view] is the mean along dim_within_view of the cluster for the view.
clusterimportances[cluster, view] is the importance of the given view to the cluster
"""
function makeclusterprefs(model::DCCModel, nviews::Int, viewdims::Vector{Int}, clustercounts::Vector{Int}, rng)
    maxviewsize = maximum(viewdims)
    clustermeans = Array{Float64, 3}(undef, maxviewsize, maximum(clustercounts), length(viewdims))
    clusterimportances = zeros(Float64, maximum(clustercounts), length(viewdims))
    for view in 1:nviews
        for cluster in 1:clustercounts[view]
            cares = rand(rng, model.caringdist) #How extreme the cluster's preferences are expected to be
            for dim in 1:viewdims[view]
                clustermeans[dim, cluster, view] = randn(rng)*sqrt(cares)
            end
            clusterimportances[cluster, view] = rand(rng, model.caringdist)
        end
    end
    return clustermeans, clusterimportances
end

"""
    makeprefpoints(model::DCCModel,
                        views::Matrix{Int},
                        viewdims::Vector{Int},
                        clustermeans::Array{Float64},
                        clusterimportances::Matrix{Float64},
                        nviews::Int, rng)

Create points in "policyspace" for all voters and candidates.

Returns (points, weights).
points: the position in policyspace of all voters and candidates
weights[dimension, voter] is the weight assigned by voter to dimension
"""
function makeprefpoints(model::DCCModel,
                        views::Matrix{Int},
                        viewdims::Vector{Int},
                        dimweights::Vector{Float64},
                        clustermeans::Array{Float64},
                        clusterimportances::Matrix{Float64},
                        nviews::Int, rng)
    npoints = size(views, 2)
    totaldims = sum(viewdims)
    points = Matrix{Float64}(undef, totaldims, npoints)
    weights = Matrix{Float64}(undef, totaldims, npoints)
    for p in 1:npoints
        dim = 1
        for view in 1:nviews
            cluster = views[view, p]
            endindex = dim + viewdims[view]-1
            centerofmass = clustermeans[1:viewdims[view], cluster, view]
            clusterweight = clusterimportances[cluster, view]
            points[dim:endindex , p] = centerofmass + (
                 randn(rng, viewdims[view]) .* sqrt(1 - clusterweight)
            )
            weights[dim:endindex , p] = dimweights[dim:endindex] .* clusterweight
            dim += viewdims[view]
        end
    end
    return points, weights
end

"""
    normalize_weights(model::DCCModel, weights)

Normalize a matrix of weights s.t. each column will have an L^2 norm of 1.
"""
function normalize_weights(model::DCCModel, weights)
    ndims, nvot = size(weights)
    weight_totals = [sqrt(sum((weights[dim, voter])^2
                    for dim in 1:ndims)) for _ in [1], voter in 1:nvot]
    weights ./= weight_totals
end

"""
    positions_to_utils(model::DCCModel, votersandcands, weights, nvot, ncand)

Convert the weights and voter/candidate positions in policyspace to a utility matrix.
"""
function positions_to_utils(model::DCCModel, votersandcands, weights, nvot, ncand)
    ndims = size(votersandcands, 1)
    weight_totals = [sum((weights[dim, voter])^2
                    for dim in 1:ndims) for voter in 1:nvot]
    utilmatrix = Matrix{Float64}(undef, ncand, nvot)
    voterpositions = view(votersandcands, :, 1:nvot)
    candpositions = view(votersandcands, :, nvot+1:nvot+ncand)
    for voter in 1:nvot
        for cand in 1:ncand
            utilmatrix[cand, voter] = -sqrt(sum(
                ((voterpositions[d, voter] - candpositions[d, cand])*weights[d, voter])^2
                for d in 1:ndims) / weight_totals[voter])
        end
    end
    return utilmatrix
end

make_electorate(model::TestModel, ::Int, ::Int, ::Int) = model.electorate
make_spatial_electorate(model::TestSpatialModel, ::Int, ::Int, ::Int) = model.electorate